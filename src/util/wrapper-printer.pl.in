#! /usr/bin/perl

=head1 NAME

wrapper-printer.pl - wrapper for printer access

=head1 SYNOPSIS

 my $id = $userdata{id};
 my $password = 'secret';
 my $app_id = Schulkonsole::Config::PRINTERINFOAPP;

 open SCRIPT, "| $Schulkonsole::Config::_wrapper_printer";
 print SCRIPT <<INPUT;
 $id
 $password
 $app_id

 INPUT

=head1 DESCRIPTION

=cut

use strict;
use lib '@datadir@/@PACKAGE@';
use Data::Dumper;
use Schulkonsole::Config;
use Schulkonsole::DB;
use Schulkonsole::Error::Printer;



my $id = <>;
$id = int($id);
my $password = <>;
chomp $password;

my $userdata = Schulkonsole::DB::verify_password_by_id($id, $password);
exit (  Schulkonsole::Error::Printer::WRAPPER_UNAUTHENTICATED_ID
      - Schulkonsole::Error::Printer::WRAPPER_ERROR_BASE)
	unless $userdata;

my $app_id = <>;
($app_id) = $app_id =~ /^(\d+)$/;
exit (  Schulkonsole::Error::Printer::WRAPPER_APP_ID_DOES_NOT_EXIST
      - Schulkonsole::Error::Printer::WRAPPER_ERROR_BASE)
	unless defined $app_id;

my $app_name = $Schulkonsole::Config::_id_root_app_names{$app_id};
exit (  Schulkonsole::Error::Printer::WRAPPER_APP_ID_DOES_NOT_EXIST
      - Schulkonsole::Error::Printer::WRAPPER_ERROR_BASE)
	unless defined $app_name;



my $permissions = Schulkonsole::Config::permissions_apps();
my $groups = Schulkonsole::DB::user_groups(
	$$userdata{uidnumber}, $$userdata{gidnumber}, $$userdata{gid});

my $is_permission_found = 0;
foreach my $group (('ALL', keys %$groups)) {
	if ($$permissions{$group}{$app_name}) {
		$is_permission_found = 1;
		last;
	}
}
exit (  Schulkonsole::Error::Printer::WRAPPER_UNAUTHORIZED_ID
      - Schulkonsole::Error::Printer::WRAPPER_ERROR_BASE)
	unless $is_permission_found;


my $opts;
SWITCH: {

=head3 printer_info

numeric constant: C<Schulkonsole::Config::PRINTERINFOAPP>

=head4 Description

Dumps code to be C<eval>ed to a hash with the printer names as keys an
as value a hash with configuration variable names as keys an their values
as values.

=head4 Parameters from standard input

none

=cut

$app_id == Schulkonsole::Config::PRINTERINFOAPP and do {
	my %value_type = (
		AllowUser => 2,
		DenyUser => 2,
		State => 1,
		StateMessage => 1,
		Info => 1,
		Location => 1,
		Accepting => 1,
	);

	open PRINTERSCONF, "<$Schulkonsole::Config::_cups_printers_conf_file"
		or exit (  Schulkonsole::Error::Printer::WRAPPER_CANNOT_OPEN_PRINTERSCONF
		         - Schulkonsole::Error::Printer::WRAPPER_ERROR_BASE);

	my $printer;
	my %printer_info;
	while (<PRINTERSCONF>) {
		if (/^<.*Printer\s+(.+)>/) {
			$printer = $1;
		} elsif (m:^</:) {
			$printer = undef;
		} elsif (    my ($name, $value) = /^(\S+)\s+(.*)$/
		         and $printer) {
			if ($value_type{$name}) {
				if ($value_type{$name} == 2) {
					$printer_info{$printer}{$name}{$value} = 1;
				} else {
					$printer_info{$printer}{$name} = $value;
				}
			}
		}
	}

	close PRINTERSCONF;

	my $data = Data::Dumper->new([ \%printer_info ]);
	$data->Terse(1);
	$data->Indent(0);
	print $data->Dump;

	exit 0;
};
$app_id == Schulkonsole::Config::PRINTERONOFFAPP and do {
	my $on = <>;
	($on) = $on =~ /^(\d)$/;

	my @printers;
	while (my $printer = <>) {
		last if $printer =~ /^$/;

		($printer) = $printer =~ /^(\S{0,127})$/;
		exit (  Schulkonsole::Error::Printer::WRAPPER_INVALID_PRINTER_NAME
		      - Schulkonsole::Error::Printer::WRAPPER_ERROR_BASE)
			unless $printer;

		push @printers, $printer
	}
	exit (  Schulkonsole::Error::Printer::WRAPPER_NO_PRINTERS
	      - Schulkonsole::Error::Printer::WRAPPER_ERROR_BASE)
		unless @printers;

	if ($on) {
		foreach my $printer (@printers) {
			system "$Schulkonsole::Config::_cmd_printer_accept \Q$printer";
		}
	} else {
		foreach my $printer (@printers) {
			system "$Schulkonsole::Config::_cmd_printer_reject \Q$printer";
		}
	}


	exit 0;
};
$app_id == Schulkonsole::Config::PRINTERALLOWDENYAPP and do {
	my %printer_users;

	while (my $printer = <>) {
		last if $printer =~ /^$/;
		($printer) = $printer =~ /^(\S{0,127})$/;
		exit (  Schulkonsole::Error::Printer::WRAPPER_INVALID_PRINTER_NAME
		      - Schulkonsole::Error::Printer::WRAPPER_ERROR_BASE)
			unless $printer;

		$printer_users{$printer} = [];
		while (my $user = <>) {
			last if $user =~ /^$/;
			($user) = $user =~ /^(\S+)$/;
			exit (  Schulkonsole::Error::Printer::WRAPPER_INVALID_USER
			      - Schulkonsole::Error::Printer::WRAPPER_ERROR_BASE)
				unless $user;

			push @{ $printer_users{$printer} }, "\Q$user";
		}
	}
	exit (  Schulkonsole::Error::Printer::WRAPPER_NO_PRINTERS
	      - Schulkonsole::Error::Printer::WRAPPER_ERROR_BASE)
		unless %printer_users;


	foreach my $printer (keys %printer_users) {
		if ( @{ $printer_users{$printer} } ) {
			system "$Schulkonsole::Config::_cmd_printer_lpadmin -p\Q$printer\E -u deny:"
				. join(',', @{ $printer_users{$printer} });
		} else {
			system "$Schulkonsole::Config::_cmd_printer_lpadmin -p\Q$printer\E -u deny:none";
		}
	}

	exit 0;
};
}



exit -2;	# program error

