#! /usr/bin/perl
#
# fschuett@gymnasium-himmelsthuer.de
# 06.08.2014
#

=head1 NAME

wrapper-radius.pl - wrapper for configuration of radius

=head1 SYNOPSIS

 my $id = $userdata{id};
 my $password = 'secret';
 my $app_id = Schulkonsole::Config::WLANONOFFAPP;

 open SCRIPT, "| $Schulkonsole::Config::_wrapper_radius";
 print SCRIPT <<INPUT;
 $id
 $password
 $app_id
 1
 teachers
 extra

 INPUT

=head1 DESCRIPTION

=cut

use strict;
use lib '@datadir@/@PACKAGE@';
use open ':utf8';
use open ':std';
use Sophomorix::SophomorixAPI;
use Sophomorix::SophomorixConfig;
use Schulkonsole::Config;
use Schulkonsole::DB;
use Schulkonsole::Encode;
use Schulkonsole::Error::Radius;
use Schulkonsole::Radius;
use Schulkonsole::LessonSession;

my $id = <>;
$id = int($id);
my $password = <>;
chomp $password;

my $userdata = Schulkonsole::DB::verify_password_by_id($id, $password);
exit (  Schulkonsole::Error::Radius::WRAPPER_UNAUTHENTICATED_ID
      - Schulkonsole::Error::Radius::WRAPPER_ERROR_BASE)
	unless $userdata;


my $app_id = <>;
($app_id) = $app_id =~ /^(\d+)$/;
exit (  Schulkonsole::Error::Radius::WRAPPER_APP_ID_DOES_NOT_EXIST
      - Schulkonsole::Error::Radius::WRAPPER_ERROR_BASE)
	unless defined $app_id;

my $app_name = $Schulkonsole::Config::_id_root_app_names{$app_id};
exit (  Schulkonsole::Error::Radius::WRAPPER_APP_ID_DOES_NOT_EXIST
      - Schulkonsole::Error::Radius::WRAPPER_ERROR_BASE)
	unless defined $app_name;



my $permissions = Schulkonsole::Config::permissions_apps();
my $groups = Schulkonsole::DB::user_groups(
	$$userdata{uidnumber}, $$userdata{gidnumber}, $$userdata{gid});

my $is_permission_found = 0;
foreach my $group (('ALL', keys %$groups)) {
	if ($$permissions{$group}{$app_name}) {
		$is_permission_found = 1;
		last;
	}
}
exit (  Schulkonsole::Error::Radius::WRAPPER_UNAUTHORIZED_ID
      - Schulkonsole::Error::Radius::WRAPPER_ERROR_BASE)
	unless $is_permission_found;


SWITCH: {

=head3 wlan_on_off

numeric constant: C<Schulkonsole::Config::WLANONOFFAPP>

=head4 Description

invokes C<<
wlan_on_off.sh --trigger=<on|off> --grouplist=<group1,group2,...,groupn>
>>


=head4 Parameters from standard input

=over

=item trigger

C<1> (on) or C<0> (off)

=item group

group names one per line, end with empty line

=back

=cut

(   $app_id == Schulkonsole::Config::WLANONOFFAPP ) and do {
	my $trigger = <>;
	$trigger = int($trigger) ? 'on' : 'off';
	my $lessongroup = <>;
	($lessongroup) = $lessongroup =~ /^([\w -]+)$/;
	exit (  Schulkonsole::Error::Radius::WRAPPER_NO_GROUPS
	      - Schulkonsole::Error::Radius::WRAPPER_ERROR_BASE)
		unless $lessongroup;

	my $cmd =  $Schulkonsole::Config::_cmd_wlan_on_off;

	my $opts;
	$opts = "--trigger=$trigger --grouplist=$lessongroup";
        
        # set ruid, so that ssh searches for .ssh/ in /root
	local $< = $>;
	local $) = 0;
	local $( = $);
	umask(022);
	exec Schulkonsole::Encode::to_cli("$cmd $opts") or last SWITCH;
};


=head3 wlan_reset

numeric constant: C<Schulkonsole::Config::WLANRESETAPP>

=head4 Description

Resets the groups settings in a group

=head4 Parameters from standard input

=over

=item C<group>

The name of the group

=back

=cut

$app_id == Schulkonsole::Config::WLANRESETAPP and do {
        my $group = <>;
        ($group) = $group =~ /^([\w -]+)$/;
        exit (  Schulkonsole::Error::Radius::WRAPPER_INVALID_GROUP
              - Schulkonsole::Error::Radius::WRAPPER_ERROR_BASE)
                unless $group;

        my $group_session = group_session($group);

        wlan_reset($group_session);

        $group_session->delete();

        exit 0;
};


=head3 wlan_reset_at

numeric constant: C<Schulkonsole::Config::WLANRESETATAPP>

=head4 Description

Resets the wlan settings for a group at a given time

=head4 Parameters from standard input

=over

=item C<group>

The name of the group

=item C<time>

Absolute time in seconds since the Epoch

=back

=cut

$app_id == Schulkonsole::Config::WLANRESETATAPP and do {
	my $lessongroup = <>;
	($lessongroup) = $lessongroup =~ /^([\w -]+)$/;
	exit (  Schulkonsole::Error::Radius::WRAPPER_INVALID_GROUP
	      - Schulkonsole::Error::Radius::WRAPPER_ERROR_BASE)
		unless $lessongroup;

	my $time = <>;
	($time) = $time =~ /^(\d+)$/;
	exit (  Schulkonsole::Error::Radius::WRAPPER_INVALID_LESSONTIME
	      - Schulkonsole::Error::Radius::WRAPPER_ERROR_BASE)
		unless $time;

	{ # write values and close session
	my $group_session = group_session($lessongroup);
	$group_session->param('end_time', $time);
	}

	my $pid = fork;
	exit (  Schulkonsole::Error::Radius::WRAPPER_CANNOT_FORK
	      - Schulkonsole::Error::Radius::WRAPPER_ERROR_BASE)
		unless defined $pid;

	if (not $pid) {
		close STDIN;
		close STDOUT;
		close STDERR;
		open STDOUT, '>>', '/dev/null' or die;
		open STDERR, '>>&', *STDOUT or die;

		sleep $time - $^T;

		my $group_session = group_session($lessongroup);

		my $stored_group = $group_session->param('name');
		exit (  Schulkonsole::Error::Radius::WRAPPER_CANNOT_READ_GROUPFILE
		      - Schulkonsole::Error::Radius::WRAPPER_ERROR_BASE)
			unless $stored_group;
		my $stored_id = $group_session->param('user_id');
		exit (  Schulkonsole::Error::Radius::WRAPPER_CANNOT_READ_GROUPFILE
		      - Schulkonsole::Error::Radius::WRAPPER_ERROR_BASE)
			unless $stored_id;
		my $stored_time = $group_session->param('end_time');
		exit (  Schulkonsole::Error::Radius::WRAPPER_CANNOT_READ_GROUPFILE
		      - Schulkonsole::Error::Radius::WRAPPER_ERROR_BASE)
			unless $stored_time;

		if (    $stored_time == $time
		    and $stored_id == $id
		    and $stored_group eq $lessongroup) {

			Schulkonsole::DB::reconnect();
			wlan_reset($group_session);

			$group_session->delete();
		}
	}

	exit 0;
};

}

exit -2;	# program error



sub group_session {
	my $lessongroup = shift;

	my $session = new Schulkonsole::LessonSession($lessongroup);

	exit (  Schulkonsole::Error::Radius::WRAPPER_GENERAL_ERROR
	      - Schulkonsole::Error::Radius::WRAPPER_ERROR_BASE)
		unless $session;

	# 'unprivileged' is set in the main CGI-script
	# do not keep a session, that we created as root
	if (not $session->param('unprivileged')) {
		$session->delete();
		exit 0;
	}

	return $session;
}




sub wlan_reset {
	my $group_session = shift;

	my $lessongroup = $group_session->param('name');

	# set ruid, so that ssh searches for .ssh/ in /root
	local $< = $>;
	local $) = 0;
	local $( = $);
	umask(022);

	# reset the group settings according to group_default instead of restoring 
	# the old state from session file
	my $opts = "--group=\Q$lessongroup\E";
	system Schulkonsole::Encode::to_cli(
	       	"$Schulkonsole::Config::_cmd_linuxmuster_wlan_reset $opts");

	return 1;
}

