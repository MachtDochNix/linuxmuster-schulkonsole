#! /usr/bin/perl

=head1 NAME

class_db - create/drop DB of a class

=head1 SYNOPSIS

 https://server/schulkonsole/class_db

=head1 DESCRIPTION

C<class_db> lets you create or drop a DB of a class.
The HTML template is class_db.shtml.

=head2 Template variables

Additionally to the variables of Schulkonsole::Session C<class_db>
provides the following variables:

=over

=cut

use strict;
use lib '@datadir@/@PACKAGE@';
use Schulkonsole::Session;
use Schulkonsole::Info;
use Schulkonsole::Collab;

my $this_file = 'class_db';


my $sk_session = new Schulkonsole::Session($this_file);
if (not $sk_session->get_password()) {
	my $q = new CGI;
	my $url = $q->url( -full => 1 );

	# we send cookies over secure connections only
	if ($url =~ s/^http:/https:/g) {
		$sk_session->redirect($url);
	} else {
		$sk_session->exit_with_login_page($this_file);
	}
}

my $q = $sk_session->query();


my $classs = Schulkonsole::Info::groups_classes($sk_session->groups());
my $class = $q->param('classes');
if (    $class
    and $$classs{$class}) {
	$sk_session->param('class', $class);
}
$class = $sk_session->param('class');

if (   not $class
    or not $$classs{$class}) {
	my $url = $q->url( -absolute => 1 );
	$url =~ s/$this_file$/class/g;
	$sk_session->redirect($url);
}


my $id = $sk_session->userdata('id');
my $password = $sk_session->get_password();



eval {
if ($q->param('create')) {
	Schulkonsole::Collab::create_db($id, $password, $class);

	$sk_session->set_status(
		sprintf($sk_session->d()->get('Datenbanken f&uuml;r %s erstellt'),
		        $class),
		0);
} elsif ($q->param('drop')) {
	Schulkonsole::Collab::drop_db($id, $password, $class);

	$sk_session->set_status(
		sprintf($sk_session->d()->get('Datenbanken f&uuml;r %s gel&ouml;scht'),
		        $class),
		0);
}
};
if ($@) {
	$sk_session->standard_error_handling($this_file, $@);
}



eval {
my $dbs = Schulkonsole::Collab::list_db($id, $password);
foreach my $db (@$dbs) {
	if ($db eq $class) {

=item C<class_db>

The name of the DB (if existent)

=cut

		$sk_session->set_var('class_db', $class);
		last;
	}
}
};
if ($@) {
	$sk_session->standard_error_handling($this_file, $@);
}


my @classs;
foreach my $class (sort {
    $$classs{$a}{displayname} cmp $$classs{$b}{displayname} } keys %$classs) {
	push @classs, { gid => $class,
	                name => $$classs{$class}{displayname} };
}

=item C<classes>

An array of hashes of the classes of which the current user is a member
with the keys

=over

=item C<gid>

The GID of the class

=item C<name>

The name of the class

=back

=cut

$sk_session->set_var('classes', \@classs);


=item C<class>

The name of the class

=cut

$sk_session->set_var('class', $$classs{$class}{displayname});

$sk_session->print_page("$this_file.shtml", $this_file);


=back

=head2 Form fields

=over

=item C<create>

Create the DB if true

=item C<drop>

Drop the DB if true

=back
